# Maintainer: Jose Riha <jose1711 gmail com>
# Maintainer: Sebastian J. Bronner <waschtl@sbronner.com>
# Contributor: Patrick Jackson <PatrickSJackson gmail com>
# Contributor: Christoph Vigano <mail@cvigano.de>

pkgname=st
pkgver=0.8.2
pkgrel=10
pkgdesc='A simple virtual terminal emulator for X.'
arch=('i686' 'x86_64' 'armv7h')
license=('MIT')
depends=(libxft)
url=http://st.suckless.org
source=(http://dl.suckless.org/$pkgname/$pkgname-$pkgver.tar.gz
        terminfo.patch
        README.terminfo.rst
        config.h
        00-solarized.diff
        01-alpha.diff)

sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  patch --strip=0 < $srcdir/terminfo.patch
  for stage in 00 01; do
    cat $srcdir/${stage}-*.diff |patch -p1
  done
  # sed -i '/tic /d' Makefile
  cp $srcdir/config.h ./
}

build() {
  cd $srcdir/$pkgname-$pkgver
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
  install -Dm644 $srcdir/README.terminfo.rst "$pkgdir/usr/share/doc/$pkgname/README.terminfo.rst"
  install -Dm644 st.info "$pkgdir/usr/share/$pkgname/st.info"
}

