# $Id: PKGBUILD 92388 2013-06-04 13:14:40Z lfleischer $
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Pierre Chapuis <catwell at archlinux dot us>
# Contributor: Ray Kohler <ataraxia937 at gmail dot com>
# Contributor: Michael Witten <mfwitten>
# Contributor: Raphael Proust <raphlalou at gmail dot com>

pkgname=surf
pkgver=0.7
pkgrel=1
pkgdesc='A simple web browser based on WebKit/GTK+.'
arch=('i686' 'x86_64')
url='http://surf.suckless.org/'
license=('MIT')
depends=('libwebkit' 'xorg-xprop')
optdepends=('dmenu: URL-bar'
            'ca-certificates: SSL verification'
            'xterm: default download handler'
            'curl: default download handler')
makedepends=('pkgconfig')
install='surf.install'
source=("git://git.suckless.org/surf"
        "surf-git-searchengines.diff"
        'config.h')
md5sums=('SKIP'
         'SKIP'
         'SKIP')
_gitname=surf

pkgver () {
  cd $srcdir/$_gitname
  git describe --tags |sed s+-+.+g
}

prepare() {
  cd "${srcdir}/${_gitname}"
  patch -Np1 -i "${srcdir}/surf-git-searchengines.diff"
  cp "${srcdir}/config.h" config.h
}

build() {
  cd "${srcdir}/${_gitname}"

  sed -i 's/CPPFLAGS =/CPPFLAGS +=/g' config.mk
  sed -i 's/CFLAGS =/CFLAGS +=/g' config.mk
  sed -i 's/LDFLAGS =/LDFLAGS +=/g' config.mk

  make
}

package() {
  cd "${srcdir}/${_gitname}"

  make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
