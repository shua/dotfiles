# Maintainer: Devin J. Pohly <djpohly+arch@gmail.com>
pkgname=dwl-git
pkgver=0.8.dev.r55.d1c2f43
pkgrel=1
pkgdesc="Simple, hackable dynamic tiling Wayland compositor (dwm for Wayland)"
arch=('x86_64')
url="https://codeberg.org/dwl/dwl"
license=('GPL')
depends=('wlroots>=0.13' 'swaylock' 'bemenu' 'grim')
makedepends=('git' 'wayland-protocols')
optdepends=('xorg-xwayland: for XWayland support')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
# append #branch=wlroots-next to build against wlroots-git
#_commit="87d87cc4041a997d00ebf234ca5118b9248a3b95"
#source=('git+https://github.com/djpohly/dwl#commit=87d87cc4041a997d00ebf234ca5118b9248a3b95'
source=('git+https://codeberg.org/dwl/dwl'
        config.h
        0001-make-protocols.patch
        0002-shiftview.patch
        0003-smartborders.patch
        0004-ipc.patch)
        # 0005-pointerconstraints.patch)
sha256sums=('SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP')

prepare() {
	cd "$srcdir/${pkgname%-git}"
	# git reset --hard --quiet bd59573
	rm -f shiftview.c protocols/dwl-ipc*
	# Use a custom config.h if the file is not empty
	if [ -s "$srcdir/config.h" ]; then
		cp -f "$srcdir/config.h" config.h
	fi
	# Uncomment to compile with XWayland support
	# sed -i -e '/^#\(XWAYLAND\|XLIB\)/s/^#//' config.mk

	git apply "$srcdir"/*.patch
}

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "%s" "$(git describe --long | sed 's/^v//;s/\([^-]*-\)g/r\1/;s/-/./g')"
}

build() {
	cd "$srcdir/${pkgname%-git}"
	make
}

package() {
	cd "$srcdir/${pkgname%-git}"
	make PREFIX="$pkgdir/usr/" install
}
