# $Id: PKGBUILD 124788 2014-12-28 22:25:00Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sebastian A. Liem <sebastian at liem dot se>

pkgname=slock
pkgver=1.2
pkgrel=1
pkgdesc="A simple screen locker for X"
arch=('i686' 'x86_64')
url="http://tools.suckless.org/slock"
license=('MIT')
depends=('libxext')
source=("http://dl.suckless.org/tools/$pkgname-$pkgver.tar.gz"
        "config.h"
        "shua.diff")
md5sums=('ee65f6e9f5ce71f3a6088d53b2485430'
         'SKIP'
         'SKIP')


prepare() {
  cd "$srcdir/slock-$pkgver"
  patch -Np1 -i "$srcdir/shua.diff"
  cp "$srcdir/config.h" ./
}

build() {
  cd "$srcdir/slock-$pkgver"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "$srcdir/slock-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
